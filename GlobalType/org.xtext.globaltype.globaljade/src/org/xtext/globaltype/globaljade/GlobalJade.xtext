grammar org.xtext.globaltype.globaljade.GlobalJade with org.eclipse.xtext.common.Terminals

generate globalJade "http://www.xtext.org/globaltype/globaljade/GlobalJade"

/*
 * file extension: gbjade
 */
Model:
	// global protocol declaration
	'global' 'protocol' name=NAME '(' roles+=Roles ')'
	'{'
		protocol+=Protocol
	'}'
;

Roles:
	roles+=Role (',' roles+=Role)*
;

Role:
	OneRole | MultipleRole
;

OneRole:
	'role' name=NAME
;

MultipleRole:
	'multrole' name=NAME ':' ref=[OneRole | NAME]
;


Protocol:
	{Protocol} (actions+=(Message
			   | Choice_rule 
			   | For_loop 
			   | End_message 
			   | Recursion 
			   | End_Recursion 
			   | Continue_Recursion
			   ))* 
;

Recursion:
	'rec' name=NAME ':'
;

End_Recursion:
	'stop' name=NAME
;

Continue_Recursion:
	'continue' name=NAME
;

Choice_rule:
	// for choice
	'choice' 'at' role_name=NAME '{'
		messages+=Message
		branch+=Protocol
	'}' ('or' '{'
		messages+=Message
		branch+=Protocol
	'}')*
;

For_loop:
	'forall' name=NAME 'in' roleTarget=NAME 'do' '{'
		(
			branch+=Protocol
		)
	'}' 
;

End_message:
	end+=END_MEX ';'
;

END_MEX:
	'end'
;

Message:
	type=TYPE_MESSAGE '('content+=CONTENT (',' content+=CONTENT)*')' 'from' sender=NAME 'to' receiver=NAME ';'
;

terminal TYPE_MESSAGE:
	'CFP' 
	| 'REJECT-PROPOSAL' 
	| 'ACCEPT-PROPOSAL' 
	| 'FAILURE' 
	| 'INFORM-DONE' 
	| 'INFORM' 
	| 'PROPOSE'
;

terminal CONTENT:
	'int' | 'bool' | 'string'
;

terminal NAME:
	('A'..'Z')+ ('a'..'z')*
;
