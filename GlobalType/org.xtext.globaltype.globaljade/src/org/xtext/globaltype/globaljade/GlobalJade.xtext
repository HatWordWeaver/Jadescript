grammar org.xtext.globaltype.globaljade.GlobalJade with org.eclipse.xtext.common.Terminals

generate globalJade "http://www.xtext.org/globaltype/globaljade/GlobalJade"

/*
 * file extension: gbjade
 */
Model:
	// global protocol declaration
	'global' 'protocol' protocolName=NAME '(' roles=Roles ')'
	'{'
		protocol=Protocol
	'}'
;

Roles:
	roles+=Role (',' roles+=Role)*
;

Role:
	RoleOne | RoleMultiple
;

RoleOne:
	'role' name=NAME
;

RoleMultiple:
	'multrole' name=NAME ':' ref=[RoleOne | NAME]
;


Protocol:
	{Protocol} (actions+=(Message
			   | Choice 
			   | ForEach 
			   | Recursion 
			   | CloseRecursion 
			   ))* 
;

Recursion:
	'rec' name=NAME ':'
;

CloseRecursion:
	'loop' recursionVariable=[Recursion |NAME] ';'
;


Choice:
	// for choice
	'choice' 'at' role=NAME '{'
		messages+=Message
		branch+=Protocol
	'}' ('or' '{'
		messages+=Message
		branch+=Protocol
	'}')*
;

ForEach:
	'foreach' eachRole=Role 'in' role=[RoleMultiple | NAME] '{'
		(
			branch=Protocol
		)
	'}' 
;

Message:
	type=MessageType '(' payload=Payload?')' 'from' sender=NAME 'to' receiver=NAME ';'
;

MessageType:
	'CFP' 
	| 'REJECT-PROPOSAL' 
	| 'ACCEPT-PROPOSAL' 
	| 'FAILURE' 
	| 'INFORM-DONE' 
	| 'INFORM' 
	| 'PROPOSE'
;

Payload:
	{Payload} (types+=Type (','types+=Type)*)
;

Type:
	'int' | 'string' | 'action'
;

terminal NAME:
	('A'..'Z')+ ('a'..'z')*
;
